import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

def getBackupFilename() {
	def formattedDate = LocalDateTime.now().format(DateTimeFormatter.ofPattern("yyyy-MM-dd"))
	def dailyBuildNumber = Integer.parseInt(BUILD_NUMBER) % 2
	def fileName = "postgres-backup-${formattedDate}-build-${dailyBuildNumber}.sql"

	echo "Created backup file name: ${fileName}"
	return fileName
}

pipeline {
	agent {
		kubernetes {
			inheritFrom 'default'
			defaultContainer 'buildpack'
		}
	}

	triggers {
		cron('H */12 * * *')
	}

	options {
		timeout(time: 15, unit: 'MINUTES')
		buildDiscarder(logRotator(numToKeepStr: '30'))
	}

	environment {
		APP_NAME = "db"
		NAMESPACE = "maxstash-global"
		POD_NAME = "${APP_NAME}-postgresql-0"
		BRANCH_NAME = 'main'
		
		KUBECONFIG = credentials('kube-config')
		POSTGRES = credentials('postgres')
	}

	stages {
		stage('Backup Database') {
			steps {
				script {
					env.BACKUP_FILE = getBackupFilename()

					echo "Starting database backup to ${BACKUP_FILE}"

					sh """
						kubectl exec ${POD_NAME} -n ${NAMESPACE} -- bash -c 'PGPASSWORD=${POSTGRES_PSW} pg_dumpall -U ${POSTGRES_USR} > /tmp/backup.sql'
						kubectl cp ${NAMESPACE}/${POD_NAME}:/tmp/backup.sql ${BACKUP_FILE}
					"""

					archiveArtifacts artifacts: "$BACKUP_FILE", fingerprint: true

					echo "Database backup completed successfully"
				}
			}
		}
	}
}
